/*
 * Copyright (c) 2018 Theodore SUPIOT
 * <theodore.supiot@gmail.com>
 * Allright reserved
 */

plugins {
    id 'java'
    id 'pmd'
    id 'findbugs'
    id 'checkstyle'
    id 'de.aaschmid.cpd' version '1.0'
    id 'jacoco'
    id 'jdepend'
}

group 'iia_m2_cours_agile'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'joda-time', name: 'joda-time', version: '2.10'
}

// PMD
pmd{
    pmdTest.enabled = false
    ignoreFailures = true
    ruleSets = ["java-basic", "java-braces", "java-clone", "java-codesize",
                "java-comments", "java-controversial", "java-coupling",
                "java-design", "java-empty", "java-finalizers", "java-imports",
                "java-naming", "java-optimizations", "java-strings",
                "java-unusedcode"]
}
tasks.withType(Pmd){
    reports {
        xml.enabled true
        html.enabled true
    }
}

// Findbugs
findbugs {
    findbugsTest.enabled = false
    ignoreFailures = true
    effort = "max"
    showProgress = true
    reportLevel = "high"
    visitors = ["FindSqlInjection", "SwitchFallthrough"]
    omitVisitors = ["FindNonShortCircuit"]
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

// Checkstyle
checkstyle{
    checkstyleTest.enabled = false
    ignoreFailures = true
    showViolations = true
    configFile file("${project.rootDir}/config/checkstyle/sun_checks.xml")
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

// CPD
cpd {
    ignoreFailures = true
}
tasks.withType(de.aaschmid.gradle.plugins.cpd.Cpd) {
    reports {
        xml.enabled = true
        text.enabled = false
    }
    source = files('src')
}

// JACOCO
jacoco {
    toolVersion = "0.8.1"
}
tasks.withType(JacocoReport) {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled true
    }
}

// JDEPEND
jdepend {
    jdependTest.enabled = false
    ignoreFailures = true
}
tasks.withType(JDepend) {
    reports {
        xml.enabled true
        text.enabled false
    }
}

test{
    ignoreFailures = true
}